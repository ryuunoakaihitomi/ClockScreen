apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

android {
    signingConfigs {
        arg {
            Properties p = new Properties()
            File f = new File(rootDir, "signInfo.properties")
            if (f.exists()) {
                p.load(new FileReader(f))
                keyAlias p['KEY_ALIAS']
                keyPassword p['KEY_PWD']
                storePassword p['STORE_PWD']
                storeFile file(p['STORE_FILE'])
            } else println 'If you want to build the app in a smaller size with AndResGuard,' +
                    'you must create signInfo.properties before.'
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "github.ryuunoakaihitomi.clockscreen"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 2
        versionName "fu"
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.arg
        }
    }
    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        exclude 'META-INF/androidx*'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'

    repositories {
        maven {
            url 'https://raw.githubusercontent.com/ryuunoakaihitomi/maven-repository/master'
        }
    }
    runtimeOnly 'github.ryuunoakaihitomi.retoast:retoast:1.7.0'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionName}_${variant.buildType.name}.apk"
    }
}

andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    keepRoot = false
    fixedResName = "x"
    mergeDuplicatedRes = true
    whiteList = [
            "R.mipmap.ic_launcher",
            "R.mipmap.ic_launcher_round"
    ]
    compressFilePattern = [
            "*.png"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.17'
    }
    digestalg = "SHA-256"
}
